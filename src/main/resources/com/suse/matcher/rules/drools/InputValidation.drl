package com.suse.matcher.rules.drools;

import java.util.TreeMap;
import com.suse.matcher.facts.HostGuest;
import com.suse.matcher.facts.Message;
import com.suse.matcher.facts.Product;
import com.suse.matcher.facts.Subscription;
import com.suse.matcher.facts.System;
import com.suse.matcher.facts.InstalledProduct;
import com.suse.matcher.facts.CurrentTime;

rule "ignoreExpiredSubscription"
    agenda-group "InputValidation"
    when
        CurrentTime($timestamp : timestamp)
        $subscription: Subscription((endDate != null && endDate < $timestamp) || (startDate != null && startDate > $timestamp))
    then
        modify($subscription) {
            ignored = true;
        }
end

rule "ignoreNotActiveSubscriptions"
    agenda-group "InputValidation"
    when
        $subscription : Subscription(startDate == null) or
        $subscription : Subscription(endDate == null)
    then
        modify($subscription) {
            ignored = true;
        }
end

rule "ignoreSubscriptionWithoutQuantity"
    agenda-group "InputValidation"
    when
        $subscription: Subscription(quantity == null)
    then
        modify($subscription) {
            ignored = true;
        }
end

rule "retractBlacklistedProduct"
    agenda-group "InputValidation"
    when
        // novell-open-enterprise-server-i386 9
        $installedProduct : InstalledProduct(productId == 955) or
        // novell-open-enterprise-server-x86_64 9
        $installedProduct : InstalledProduct(productId == 957) or
        // open_enterprise_server 11
        $installedProduct : InstalledProduct(productId == 1232) or
        // open_enterprise_server 11.1
        $installedProduct : InstalledProduct(productId == 1241) or
        // open_enterprise_server 11.2
        $installedProduct : InstalledProduct(productId == 1242)
    then
        retract($installedProduct);
end

rule "retractFreeProduct"
    agenda-group "InputValidation"
    when
        Product($productId : id, free == true)
        $installedProduct : InstalledProduct(productId == $productId)
    then
        retract($installedProduct);
end

rule "logUnknownPartNumber"
    agenda-group "InputValidation"
    when
        $subscription: Subscription(policy == null, partNumber != null)
    then
        TreeMap<String, String> data = new TreeMap<>();
        data.put("part_number", $subscription.partNumber);
        insert(new Message(Message.Level.DEBUG, "unknown_part_number", data));
end

rule "logPhysicalGuest"
    agenda-group "InputValidation"
when
    HostGuest($guestId : guestId)
    $guest : System(id == $guestId, physical == true)
then
    // input data says this guest is physical machine, this is not expected
    // log and autocorrect
    TreeMap<String, String> data = new TreeMap<>();
    data.put("id", $guestId.toString());
    insert(new Message(Message.Level.DEBUG, "physical_guest", data));
    modify($guest) {
        physical = false;
    }
end

rule "logMissingHost"
    agenda-group "InputValidation"
when
    $guest : System($guestId : id, physical == false)
    not HostGuest(guestId == $guestId)
then
    // input data says this guest is virtual machine but we do not know
    // about the system that hosts it
    // We treat this as a physical machine and warn the user, as the
    // matching will almost surely be incorrect
    TreeMap<String, String> data = new TreeMap<>();
    data.put("id", $guestId.toString());
    insert(new Message(Message.Level.WARNING, "guest_with_unknown_host", data));
    modify($guest) {
        physical = true;
    }
end

rule "logMissingCpuCount"
    agenda-group "InputValidation"
when
    $system : System($id : id, cpus == null)
then
    TreeMap<String, String> data = new TreeMap<>();
    data.put("id", $id.toString());
    insert(new Message(Message.Level.WARNING, "unknown_cpu_count", data));
    modify($system) {
        cpus = 16;
    }
end
